/*
 * Copyright 2017 L4 Digital LLC. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'kotlin'
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'maven-publish'

def pluginUrl = 'https://github.com/L4Digital/android-test-helper-gradle-plugin'
def projectId = 'l4-android-ui-test'
def groupIdString = 'com.l4digital.plugin'
def artifactIdString = projectId
version = '0.0.1'

buildscript {
    ext.kotlin_version = '1.1.4-3'
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.8"
    }
}
repositories {
    jcenter()
    mavenLocal()
}
dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compileOnly gradleApi()
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
pluginBundle {
    website = pluginUrl
    vcsUrl = pluginUrl
    description = 'A Gradle plugin for common ADB actions primarily focused around tasks needed for UI automation.'
    tags = ['adb', 'android']

    plugins {
        adbPlugin {
            id = projectId
            displayName = 'Android UI Test Helper'
        }
    }
}
publishing {
    publications {
        mavenJar(MavenPublication) {
            groupId groupIdString
            artifactId artifactIdString
            artifact "build/libs/${project.name}-${version}.jar"
        }
    }
}

// The properties file name is used for applying the plugin in projects (apply plugin: 'properties-file-name')
// Make sure it stays in sync with the artifactId to decrease confusion.
task syncPropertiesFileNameWithArtifactId {
    doLast {
        def propertiesFileDir = 'src/main/resources/META-INF/gradle-plugins/'
        def currentPropertiesFile = file(propertiesFileDir).listFiles().first()
        def renamedFile = file("${propertiesFileDir}${artifactIdString}.properties")

        currentPropertiesFile.renameTo(renamedFile)
    }
}